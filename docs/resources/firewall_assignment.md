---
page_title: "latitudesh_firewall_assignment Resource - terraform-provider-latitudesh"
subcategory: ""
description: |-
  
---

# latitudesh_firewall_assignment (Resource)

The firewall assignment resource allows you to assign firewalls to servers within your [Latitude.sh](https://latitude.sh/) account.

## Example usage

```hcl
resource "latitudesh_project" "project" {
  name              = "Production Environment"
  environment       = "Production"
  provisioning_type = "on_demand"
}

resource "latitudesh_server" "web_server" {
  billing          = "monthly"
  hostname         = "web-01"
  operating_system = "ubuntu_22_04_x64_lts"
  plan             = "c2-medium-x86"
  project          = latitudesh_project.project.id
  site             = "SAO2"
}

resource "latitudesh_firewall" "web_firewall" {
  name    = "Web Server Firewall"
  project = latitudesh_project.project.id

  rules {
    from     = "0.0.0.0/0"
    to       = "0.0.0.0/0"
    port     = "22"
    protocol = "tcp"
  }

  rules {
    from     = "0.0.0.0/0"
    to       = "0.0.0.0/0"
    port     = "80"
    protocol = "tcp"
  }
}

resource "latitudesh_firewall_assignment" "web_assignment" {
  firewall_id = latitudesh_firewall.web_firewall.id
  server_id   = latitudesh_server.web_server.id
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `firewall_id` (String) The ID of the firewall
- `server_id` (String) The ID of the server to assign the firewall to

### Read-Only

- `id` (String) The ID of this resource.

## Import

You can import a Firewall Assignment resource using either the CLI method or the [experimental import block](https://developer.hashicorp.com/terraform/language/import).

**CLI Import**

The `latitudesh_firewall_assignment` resource can be imported by specifying the [Firewall assignment](https://docs.latitude.sh/reference/get-firewall-assignments) ID:

```sh
terraform import latitudesh_firewall_assignment.web_assignment <FW_ASSIGNMENT_ID>
```

**Alternative: Composite ID Format**

You can also import using the composite ID format (firewall ID and firewall assignment ID separated by a colon):

```sh
terraform import latitudesh_firewall_assignment.web_assignment <FIREWALL_ID>:<FW_ASSIGNMENT_ID>
```

**Import Block (Experimental)**

Terraform v1.5.0 and later supports the experimental import block, which allows you to define imports in your configuration. This feature is experimental and may change in future Terraform releases. See the [Terraform documentation for details](https://developer.hashicorp.com/terraform/language/import).

To use this method, create a file named `import.tf` (or any `.tf` file) with the following content:

```hcl
import {
  to = latitudesh_firewall_assignment.web_assignment
  id = "<FW_ASSIGNMENT_ID>"
}
```

Or, using the composite ID format:

```hcl
import {
  to = latitudesh_firewall_assignment.web_assignment
  id = "<FIREWALL_ID>:<FW_ASSIGNMENT_ID>"
}
```

Then run:

```sh
terraform plan -generate-config-out=generated_firewall_assignment.tf
```

This will generate the resource configuration for the imported firewall assignment resource.

> **Note:** The import block feature is experimental and its syntax or behavior may change in future Terraform versions. 
